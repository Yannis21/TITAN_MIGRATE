//ENHMERWSH KINHSEWN PELATWN STA ARXEIA MHNWN KAI HMEROMHNIWN

//******************************DDO TREE*********************************
//   COMPANY
//      |
//      |
//    BRANCH
//      |
//      |
//     YEAR           CUSTPROM
//      |                |
//      |                |
//      |                |
//      |                |
//      |-------------CUSTOMER
//                    |
//                    c
//                    |
//               CMHNES
//                  |
//                  c
//                  |
//               CHMNIES
//******************************DDO TREE*********************************

    Use COMPANY.DD
    Use BRANCH.DD
    Use YEAR.DD
    Use CTYPKIN.DD
    Use CUSTPROM.DD
    Use CUSTOMER.DD
    Use CMOVES.DD
    Use CMHNES.DD
    Use CHMNIES.DD

Object oUpdateMhnesHmnies is a BusinessProcessSub

       Set Display_Error_State     to True
       Set Status_Log_State        to True

       Property Date    pdHmnia_BFb   Public 0 //PALIA HMEROMHNIA
       Property Date    pdHmnia_AFb   Public 0 //NEA HMEROMHNIA
       Property Number  pnX0_BFb      Public 0 //PALIA XREWSH
       Property Number  pnP0_BFb      Public 0 //PALIA PISTWSH
       Property Number  pnX0_AFb      Public 0 //NEA XREWSH
       Property Number  pnP0_AFb      Public 0 //NEA PISTWSH
       Property Integer piBackoutBpo  Public 0 //FLAG AN 8A TRE3EI H BACKOUT
       Property Integer piUpdateBpo   Public 0 //FLAG AN 8A TRE3EI H UPDATE
       Property Integer piCust_Rec0   Public 0 // TIMH TOY CUSTOMER_REC0
       Property Integer piStatus      Public 0 //=0: EVERYTHING OK
                                               //<>0:ERROR DURING UPDATE OF CHMNIES

//************************ Data Dictionaries Structure ******************

        Object Company_DD is a Company_DataDictionary
        End_Object    // Company_DD

        Object Branch_DD is a Branch_DataDictionary
            Set DDO_Server to (Company_DD(Current_Object))
        End_Object    // Branch_DD

        Object Year_DD is a Year_DataDictionary
            Set DDO_Server to (Branch_DD(Current_Object))
        End_Object    // Year_DD

        Object Custprom_DD is a Custprom_DataDictionary
        End_Object    // Custprom_DD

        Object Customer_DD is a Customer_DataDictionary
            Set DDO_Server to (Year_DD(Current_Object))
            Set DDO_Server to (Custprom_DD(Current_Object))
            //BEGIN-END CONSTRAINS GIATI YPARXEI PROCEDURE CONSTRAIN GIA COMPANY+BRANCH+YEAR
            Begin_Constraints
                Constrain Customer Relates to Year
            End_Constraints
        End_Object    // Customer_DD

        Object Cmhnes_DD is a Cmhnes_DataDictionary
            Set DDO_Server to (Customer_DD(Current_Object))
            //BEGIN-END CONSTRAINS GIATI YPARXEI PROCEDURE CONSTRAIN GIA COMPANY+BRANCH+YEAR
            Begin_Constraints
                Constrain Cmhnes Relates to Customer
            End_Constraints
        End_Object    // Cmhnes_DD

        Object Chmnies_DD is a Chmnies_DataDictionary
            Set DDO_Server to (Cmhnes_DD(Current_Object))
            //BEGIN-END CONSTRAINS GIATI YPARXEI PROCEDURE CONSTRAIN GIA COMPANY+BRANCH+YEAR
            Begin_Constraints
                Constrain Chmnies Relates to cMhnes
            End_Constraints
        End_Object    // Chmnies_DD


//***************** End of Data Dictionaries Structure ******************

 //EYRESH TRIADAS COMPANY+BRANCH+YEAR
 Function fiBres_Company_Branch_Year Integer liCompanyDDo Integer liBranchDDo Integer liYearDDo Returns Integer
    //SEND CLEAR KANOYME MONO STO YEAR GIATI DEN YPARXEI CONSTRAIN
    //KAI AN KANOYME CLEAR STO BRANCH XANOYME THN TIMH APO TO COMPANY
    //KAI AN KANOYME META CLEAR STO YEARDDo XANOYME TIS TIMES APO COMPANY+BRANCH
    Send Clear to liYearDDo

    Move gs_Selected_Company_Code to Company.Company_Code
    Send Find to liCompanyDDo eq 1 //Company_Code

    Move gs_Selected_Branch_Code to Branch.Branch_Code
    Send Find to liBranchDDo eq 1 //Company_Code+Branch_Code

    Move gi_Selected_Year to Year.Year_Year
    Send Find to liYearDDo eq 1 //Company_Code+Branch_Code+Year_Year

    Send Rebuild_Constraints to (customer_dd(Self))
    Send Rebuild_Constraints to (cmhnes_dd(Self))
    Send Rebuild_Constraints to (chmnies_dd(Self))

    [Finderr] Function_Return 0
              Function_Return (Current_Record(liYearDDo))
 End_Function //fiBres_Company_Branch_Year

  //EYRESH MHNA. AN DEN YPARXEI, DHMIOYRGOYME NEO ME THN YPARXOYSA TIMH
  //  liCmhnesDDO,liCmhnes_Mhnas TOPIKES METABLHTES
 Function fiBres_Mhna Integer liCmhnesDDO String lsCustprom_Code Integer liCmhnes_Mhnas Returns Integer
    Send Clear to liCMhnesDDo
    Move gs_Selected_Company_Code to Cmhnes.Company_Code
    Move gs_Selected_Branch_Code to Cmhnes.Branch_Code
    Move gi_Selected_Year to Cmhnes.Year_Year
    Move lsCustprom_Code to Cmhnes.Custprom_Code
    Move liCMhnes_Mhnas to CMhnes.Cmhnes_Mhnas
    Send Find to liCMhnesDDo eq 1 //company_code+branch_code+year_year+custprom_code+cmhnes_mhnas
    [Finderr] Function_Return 0
              Function_Return (Current_Record(liCmhnesDDo))
 End_Function //fiBres_Mhna

 //EYRESH HMEROMHNIAS. AN DEN YPARXEI, DHMIOYRGOYME NEA ME THN YPARXOYSA TIMH
 Function fiBres_Hmnia Integer liChmniesDDO String lsCustprom_Code Integer liCMhnes_Mhnas Integer liChmnies_hmnia Returns Integer
    Send Clear to liCHmniesDDo
    Move gs_Selected_Company_Code to CHmnies.Company_Code
    Move gs_Selected_Branch_Code to CHmnies.Branch_Code
    Move gi_Selected_Year to CHmnies.Year_Year
    Move lsCustprom_Code to CHmnies.Custprom_Code
    Move liCMhnes_Mhnas to Chmnies.Cmhnes_Mhnas
    Move liCHmnies_Hmnia to CHmnies.CHmnies_Hmnia
    Send Find to liChmniesDDo eq 1 //company_code+branch_code+year_year+custprom_code+cmhnes_mhnas+chmnies_hmnia
    [Finderr] Function_Return 0
              Function_Return (Current_Record(liChmniesDDo))
 End_Function //fiBres_Hmnia


 //PERASMA PALAIWN TIMWN (AYTWN POY 8A AFAIRE8OYN)
 Procedure DoSetValuesForBackout Date ldHmnia_BF;
                                 Number lnX0_BF;
                                 Number lnP0_BF;

            Set pdHmnia_BFb         to ldHmnia_BF
            Set pnX0_BFb            to lnX0_BF
            Set pnP0_BFb            to lnP0_BF



       End_Procedure // DoSetValuesForBackout

 //PERASMA NEWN TIMWN (AYTWN POY 8A PROSTE8OYN)
 Procedure DoSetValuesForUpdate Date ldHmnia_AF;
                                Number lnX0_AF;
                                Number lnP0_AF;

            Set pdHmnia_AFb         to ldHmnia_AF
            Set pnX0_AFb            to lnX0_AF
            Set pnP0_AFb            to lnP0_AF

       End_Procedure // DoSetValuesForUpdate


 Function Trexe Integer liBackout;
                Integer liUpdate;
                Integer liCust_Rec0;
                Returns Integer

    Local Integer liStatus

    Set piBackoutBpo        to liBackout
    Set piUpdateBpo         to liUpdate
    Set piCust_Rec0        to liCust_Rec0

    SEND DOPROCESS // KLHSH THS DoUpdateMhnesHmnies

    //MHDENISMOS TWN PROPERTIES GIA NEES TIMES
    Send DoSetValuesForUpdate "" 0 0
    Send DoSetValuesForUpdate "" 0 0

    Get piStatus to liStatus
    Set piStatus to 0 //MHDENISMOS GIS EPOMENO TRE3IMO
    Function_Return liStatus //=0 OLA OK, <>0 ERROR
 End_Function //Trexe


///////////////////////////////////////////////////////////////////////
////////////////////Main procedure of the Bpo//////////////////////////
///////////////////////////////////////////////////////////////////////
Procedure DoUpdateMhnesHmnies
    Local Date ldHmnia_BFb   ldHmnia_AFb

    Local Integer liCust_Rec0

    Local Number lnX0_BFBpo lnP0_BFBpo;
                 lnX0_AFBpo lnP0_AFBpo

    Local Number lnOldX0 lnOldP0

    Local Integer liCompanyDDo  liBranchDDo   liYearDDo;
                  liCustpromDDo liCustomerDDo liCMhnesDDo;
                  liCHmniesDDo

    Local Integer liBackout liUpdate
    Local Integer liCmhnes_Mhnas liMhnes_Recnum liHmnies_Recnum liYear_Recnum liErr;
                  liOldCHmnies_NoRec liOldCMhnes_NoRec lsCustprom_Code

 //*** BEGIN Get values from Properties to local variables****
    Get pdHmnia_BFb           to ldHmnia_BFb
    Get pdHmnia_AFb           to ldHmnia_AFB
    Get pnX0_BFb              to lnX0_BFBpo
    Get pnP0_BFb              to lnP0_BFBpo
    Get pnX0_AFb              to lnX0_AFBpo
    Get pnP0_AFb              to lnP0_AFBpo
    Get piBackoutBpo          to liBackout
    Get piUpdateBpo           to liUpdate
    Get piCust_Rec0           to liCust_Rec0
 //*** END Get values from Properties to local variables******

 //*** BEGIN Get the object ID of each DD in local variables ***
    Move (Company_DD(self))     to liCompanyDDo
    Move (Branch_DD(self))      to liBranchDDo
    Move (Year_DD(self))        to liYearDDo
    Move (Custprom_DD(self))    to liCustpromDDo
    Move (Customer_DD(self))    to liCustomerDDo
    Move (CMhnes_DD(self))      to liCMhnesDDo
    Move (CHmnies_DD(self))     to liCHmniesDDo
 //*** END Get the object ID of each DD in local variables ***




 //LOGIKOI ELEGXOI GIA PERASMA VALID DATA STHN BPO
 If (;
     (;
       ((ldHmnia_BFb =0) and (liBackout<>0)) or;
       ((ldHmnia_BFb<>0) and (liBackout =0));
     ) or;
    ( ;
       ((ldHmnia_AFb =0) and (liUpdate<>0)) or;
       ((ldHmnia_AFb<>0) and (liUpdate =0));
    ) ;
   ) Begin
        Set piStatus to 100 //LAN8ASMENH XRHSH PARAMETRWN
        Error 1900 "BPO-DoUpMoHm: –≈—¡”Ã¡ À¡Õ»¡”Ã≈ÕŸÕ ‘…ÃŸÕ ”‘«Õ BPO"
        Procedure_Return
     End


 //EYRESH TRIADAS COMPANY+BRANCH+YEAR
 Move (fiBres_Company_Branch_Year(Self,liCompanyDDo,liBranchDDo, liYearDDo)) to liYear_Recnum

 If liYear_Recnum Eq 0 Begin
    Set piStatus to 101 //LAN8ASMENH XRHSH PARAMETRWN
    Error 1901 "BPO-DoUpMoHm: ƒ≈Õ ¬—≈»« ≈ ≈‘¡…—≈…¡, ’–œ /Ã¡, ≈‘œ” ◊—«”«”"
    Procedure_Return
 End

 //EYRESH PELATH (PREPEI OPWSDHPOTE NA YPARXEI!!!)
 Send Clear to liCustomerDDo //SOS!!! KANEI CLEAR OLA TA APO PANW MH CONSTRAINED
 Move liCust_Rec0 to Customer.Customer_Rec0
 Send Find to liCustomerDDo eq  2 //Customer_rec0
 [Finderr] Begin
            Set piStatus to 102 //LAN8ASMENH XRHSH PARAMETRWN
            Error 1902 "BPO-DoUpMoHm: ƒ≈Õ ≈◊≈…  ¡‘¡◊Ÿ—«»≈… ¡’‘œ” œ –≈À¡‘«”"
            Procedure_Return
           End


  //PAIRNOYME TON KWDIKO TOY SYNALLASSOMENOY GIA TON XRHSIMOPOIHSOYME PARAKATW
  //STIS FUNCTION EYRESHS MHNA KAI HMNIAS
  Get Field_current_value of liCustpromDDo  FIELD Custprom.Custprom_Code to lsCustprom_Code

 //@@@@@@ EXOYN CURRENT_RECORD KAI OLA TA APO PANW @@@@@@
 IF ldHmnia_BFb Ne 0 ;
    Begin //ARXH AFAIRESHS PALAIWN TIMWN APO PALIA HMEROMHNIA

        Move (TheMonthSegment(ldHmnia_Bfb)) to liCmhnes_Mhnas
        //EYRESH PALAIOY MHNA (PREPEI OPWSDHPOTE NA YPARXEI!!!)
        Move (fiBres_Mhna(Self,liCMhnesDDo,lsCustprom_Code,liCMhnes_Mhnas)) to liMhnes_Recnum

        If liMhnes_Recnum Eq 0 Begin
                                Set piStatus to 103 //LAN8ASMENH XRHSH PARAMETRWN
                                Error 1903 "BPO-DoUpMoHm: ƒ≈Õ ’–¡—◊≈…  ¡‘¡◊Ÿ—«”« √… ¡’‘œÕ ‘œ Ã«Õ¡"
                                Procedure_Return
                               End

        //EYRESH PALAIAS HMEROMHNIAS (PREPEI OPWSDHPOTE NA YPARXEI!!!)
        Move (fiBres_Hmnia(Self,liCHmniesDDo,lsCustprom_Code,liCMhnes_Mhnas,ldHmnia_BFb   )) to liHmnies_Recnum

        If liHmnies_Recnum Eq 0 Begin
                                 Set piStatus to 104 //LAN8ASMENH XRHSH PARAMETRWN
                                 Error 1904 "BPO-DoUpMoHm: ƒ≈Õ ’–¡—◊≈…  ¡‘¡◊Ÿ—«”« √… ¡’‘HN ‘HN HMEROMHNIA"
                                 Procedure_Return
                                End


        //AFAIRESH PALAIWN TIMWN (DELETE+EDIT)
        Get Field_current_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sx0     to lnOldX0
        Get Field_current_value of liCHmniesDDo  FIELD Chmnies.CHmnies_Sp0     to lnOldP0
        Get Field_current_value of liCHmniesDDo  FIELD CHmnies.CHmnies_NoRec   to liOldCHmnies_NoRec

        Set Field_changed_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sx0     to (lnOldX0 - lnX0_BFBpo)
        Set Field_changed_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sp0     to (lnOldP0 - lnP0_BFBpo)
        Set Field_changed_value of liCHmniesDDo  FIELD CHmnies.CHmnies_NoRec   to (liOldCHmnies_NoRec - 1)

        Get Request_Validate of liCHmniesDDo to liErr
        If lierr Begin
                  Set piStatus to 105 //LAN8ASMENH XRHSH PARAMETRWN
                  Error 1905 "BPO-DoUpMoHm HMRVBF: À¡»œ” ”‘œÕ ≈À≈√◊œ ≈√ ’—œ‘«‘¡” ¡–œ»« ≈’”«” ‘«” ≈√√—¡÷«”"
                  Procedure_Return
                 End //lierr

        Send Request_Save to liCHmniesDDo
        [Err] Begin
               Set piStatus to 106 //LAN8ASMENH XRHSH PARAMETRWN
               Error 1906 "BPO-DoUpMoHm HMRSBF: À¡»œ”  ¡‘¡ ‘«Õ ¡–œ»« ≈’”« ‘«” ≈√√—¡÷«”"
               Procedure_Return
              End //Err

    End //TELOS AFAIRESHS PALAIWN TIMWN APO PALIA HMEROMHNIA


 IF ldHmnia_AFb Ne 0 ;
    Begin //ARXH PROS8ESHS NEWN TIMWN STH NEA HMEROMHNIA

        Move (TheMonthSegment(ldHmnia_Afb)) to liCMhnes_Mhnas

        //EYRESH NEOY MHNA
        Move (fiBres_Mhna(Self,liCMhnesDDo,lsCustprom_Code,liCMhnes_Mhnas)) to liMhnes_Recnum

        If liMhnes_Recnum Eq 0 Begin //AN DEN BRW RECORD TO DHMIOYRGW
            Set field_changed_value of liCMhnesDDo FIELD CMhnes.CMhnes_Mhnas to liCMhnes_Mhnas

            Get Request_Validate of liCMhnesDDo to liErr
            If lierr Begin
                      Set piStatus to 107 //LAN8ASMENH XRHSH PARAMETRWN
                      Error 1907 "BPO-DoUpMoHm MHRVAF: À¡»œ” ”‘œÕ ≈À≈√◊œ ≈√ ’—œ‘«‘¡” ¡–œ»« ≈’”«” ‘«” ≈√√—¡÷«”"
                      Procedure_Return
                     End

            Send Request_Save to liCMhnesDDo
            [Err] Begin
                   Set piStatus to 108 //LAN8ASMENH XRHSH PARAMETRWN
                   Error 1908 "BPO-DoUpMoHm MHRSAF: À¡»œ”  ¡‘¡ ‘«Õ ¡–œ»« ≈’”« ‘«” ≈√√—¡÷«”"
                   Procedure_Return
                  End
        End //TELOS DHMIOYRGIAS NEOY RECORD STO CMHNES

        //EYRESH NEAS HMEROMHNIAS
        Move (fiBres_Hmnia(Self,liCHmniesDDo,lsCustprom_Code,liCMhnes_Mhnas,ldHmnia_AFb   )) to liHmnies_Recnum

        //AN DEN BRW RECORD DHMIOYRGW EGGRAFH ME TH NEA HMEROMHNIA
        If liHmnies_Recnum Eq 0;
            Set field_changed_value of liCHmniesDDo FIELD CHmnies.CHmnies_Hmnia to ldHmnia_AFb

        Get Field_current_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sx0     to lnOldX0
        Get Field_current_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sp0     to lnOldP0
        Get Field_current_value of liCHmniesDDo  FIELD CHmnies.CHmnies_NoRec   to liOldCHmnies_NoRec

        Set Field_changed_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sx0     to (lnOldX0 + lnX0_AFBpo)
        Set Field_changed_value of liCHmniesDDo  FIELD CHmnies.CHmnies_Sp0     to (lnOldP0 + lnP0_AFBpo)
        Set Field_changed_value of liCHmniesDDo  FIELD CHmnies.CHmnies_NoRec   to (liOldCHmnies_NoRec + 1)

        Get Request_Validate of liCHmniesDDo to liErr
        If lierr Begin
                  Set piStatus to 109 //LAN8ASMENH XRHSH PARAMETRWN
                  Error 1909 "BPO-DoUpMoHm HMRVAF: À¡»œ” ”‘œÕ ≈À≈√◊œ ≈√ ’—œ‘«‘¡” ¡–œ»« ≈’”«” ‘«” ≈√√—¡÷«”"
                  Procedure_Return
                 End //lierr

       Send Request_Save to liCHmniesDDo
       [Err] Begin
              Set piStatus to 110 //LAN8ASMENH XRHSH PARAMETRWN
              Error 1910 "BPO-DoUpMoHm HMRSAF: À¡»œ”  ¡‘¡ ‘«Õ ¡–œ»« ≈’”« ‘«” ≈√√—¡÷«”"
              Procedure_Return
             End //Err

    End //TELOS PROS8ESHS NEWN TIMWN STH NEA HMEROMHNIA


End_Procedure //DoUpdateMhnesHmnies

Procedure OnProcess
    Send DoUpdateMhnesHmnies
End_Procedure

End_Object //oUpdateMhnesHmnies

